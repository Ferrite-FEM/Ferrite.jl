{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Kirchhoff-Love Plate Equation\n",
    "In this example, we solve the Kirchhoff-Love equation for thin plates. This is a fourth-order partial differential equation used to model the deflection $w$ of a plate subject to transverse loading.\n",
    "The governing biharmonic equation is:\n",
    "$$\n",
    "D \\Delta^2 w = q\n",
    "$$\n",
    "where $\\Delta$ is the Laplacian operator, and D is the flexural rigidity defined by the Young's modulus $E$, thickness $t$, and Poisson's ratio $\\nu$:\n",
    "$$\n",
    "D = \\frac{Et^3}{12(1-\\nu^2)}\n",
    "$$\n",
    "In this tutorial we will model a simply supported plate, for which the boundary conditions on $\\Gamma$ are:\n",
    "$$\n",
    "w = 0\n",
    "$$\n",
    "For the weak form, we define the curvature vector $\\boldsymbol{\\kappa}$ and the constitutive matrix $\\boldsymbol{C}$:\n",
    "$$\n",
    "\\boldsymbol{\\kappa}(w) = \\begin{bmatrix} \\frac{\\partial^2 w}{\\partial x^2} \\frac{\\partial^2 w}{\\partial x \\partial y} \\\\ \\frac{\\partial^2 w}{\\partial x \\partial y} \\ \\frac{\\partial^2 w}{\\partial y^2} \\end{bmatrix}, \\quad\n",
    "\\mathbf{C} = D \\begin{bmatrix} 1 & \\nu & 0 \\\\ \\nu & 1 & 0 \\\\ 0 & 0 & \\frac{1-\\nu}{2} \\end{bmatrix}\n",
    "$$\n",
    "We use the following weak formulation:\n",
    "$$\n",
    "\\int_\\Omega \\boldsymbol{\\kappa}(\\delta w):\\mathbf{C}:\\boldsymbol{\\kappa}(w) \\, d\\Omega - \\int_\\Omega \\delta w \\cdot q \\, d\\Omega = 0 \\quad \\forall \\delta w \\in H^2_0(\\Omega)\n",
    "$$\n",
    "To enforce the simply supported boundary conditions, we use a penalty approach to impose the constraint $w = 0$. The reason for this choice will be made clear later in the tutorial. The penalty term adds the following contribution to the weak form:\n",
    "$$\n",
    "\\rho\\int_\\Gamma w \\delta w \\, d\\Gamma\n",
    "$$\n",
    "with $\\rho$ being the penalty stiffness.\n",
    "\n",
    "## Notes on FE-approximation of the biharmonic equation\n",
    "Since this weak form involves second-order derivatives (the Hessian) of the shape functions, the standard $C^0$ Lagrange elements are not suitable for discretising the finite element space. Instead, one must either use a $C^0$ interior penalty (C0IP) approach or employ finite elements that provide $C^1$ continuity.\n",
    "Here, we demonstrate the latter approach and use the fifth-order Argyris element. This is a Hermite-type element with additional degrees of freedom at the vertices and along the edges that represent the gradient and Hessian of the deflection field \\(w\\)."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Code\n",
    "The following code followes a standard Ferrite solution procedure."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Ferrite\n",
    "using SparseArrays"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "First we define some parameters"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "Grid{2, Triangle, Float64} with 800 Triangle cells and 441 nodes"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "L = 2.0         # Side length\n",
    "q0 = 10000.0    # Load\n",
    "E = 200.0e9       # Stiffness\n",
    "t = 0.01        # Thickness\n",
    "ν = 0.3         # Poisson's radtio\n",
    "penalty = 1.0e12  # Penalty stiffness\n",
    "D = (E * t^3) / (12 * (1 - ν^2)) # Flexural stiffness\n",
    "C_voigt = D * [\n",
    "    1.0 ν 0.0;\n",
    "    ν 1.0 0.0;\n",
    "    0.0 0.0 (1 - ν) / 2\n",
    "]\n",
    "C = fromvoigt(SymmetricTensor{4, 2}, C_voigt)\n",
    "\n",
    "grid = generate_grid(Triangle, (20, 20), Vec((0.0, 0.0)), Vec((L, L)))"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "We use the Argyris interpolation as and FE approximation."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "DofHandler{2, Grid{2, Triangle, Float64}}\n  Fields:\n    :w, Argyris{RefTriangle, 5}()\n  Dofs per cell: 21\n  Total dofs: 3886"
     },
     "metadata": {},
     "execution_count": 3
    }
   ],
   "cell_type": "code",
   "source": [
    "ip = Argyris{RefTriangle, 5}()\n",
    "dh = DofHandler(grid)\n",
    "add!(dh, :w, ip)\n",
    "close!(dh)"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "For the CellValues and FacetValues we need to requeest to update the hessians."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "qr = QuadratureRule{RefTriangle}(8)\n",
    "cellvalues = CellValues(qr, ip; update_hessians = true);\n",
    "\n",
    "fqr = FacetQuadratureRule{RefTriangle}(8)\n",
    "facetvalues = FacetValues(fqr, ip; update_hessians = true);"
   ],
   "metadata": {},
   "execution_count": 4
  },
  {
   "cell_type": "markdown",
   "source": [
    "For the current BVP, There is a known analtyical solution (Navier's solution) that we can compare with.\n",
    "This is a good testing strategy for PDE codes and known as the method of manufactured solutions."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function w_analytical(pos::Vec{2}, L, q0, D; n_terms = 50)\n",
    "    x, y = pos\n",
    "    w = 0.0\n",
    "    constant_factor = (16 * q0 * L^4) / (D * pi^6)\n",
    "\n",
    "    for m in 1:2:n_terms\n",
    "        for n in 1:2:n_terms\n",
    "            denom = m * n * (m^2 + n^2)^2\n",
    "            num = sin(m * pi * x / L) * sin(n * pi * y / L)\n",
    "            w += num / denom\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return constant_factor * w\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 5
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now we define the element routine."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "element_routine! (generic function with 1 method)"
     },
     "metadata": {},
     "execution_count": 6
    }
   ],
   "cell_type": "code",
   "source": [
    "function element_routine!(ke, fe, cellvalues, C, q0)\n",
    "    for iqp in 1:getnquadpoints(cellvalues)\n",
    "        dV = getdetJdV(cellvalues, iqp)\n",
    "        for i in 1:getnbasefunctions(cellvalues)\n",
    "            v = shape_value(cellvalues, iqp, i)\n",
    "            fe[i] += (q0 * v) * dV\n",
    "            δκ = shape_hessian(cellvalues, iqp, i)\n",
    "            for j in 1:getnbasefunctions(cellvalues)\n",
    "                Δκ = shape_hessian(cellvalues, iqp, j)\n",
    "                ke[i, j] += (δκ ⊡ C ⊡ Δκ) * dV\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 6
  },
  {
   "cell_type": "markdown",
   "source": [
    "To enforce the boundary condition, we use the penalty method. Currently the ConstraintHandler does not fully support Dirichlet constraints on Hermitian elements (like Argyris)."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "bc_routine! (generic function with 1 method)"
     },
     "metadata": {},
     "execution_count": 7
    }
   ],
   "cell_type": "code",
   "source": [
    "function bc_routine!(ke, facetvalues, penalty)\n",
    "    for iqp in 1:getnquadpoints(facetvalues)\n",
    "        dV = getdetJdV(facetvalues, iqp)\n",
    "        for i in 1:getnbasefunctions(facetvalues)\n",
    "            Ni = shape_value(facetvalues, iqp, i)\n",
    "            for j in 1:getnbasefunctions(facetvalues)\n",
    "                Nj = shape_value(facetvalues, iqp, j)\n",
    "                ke[i, j] += penalty * (Ni * Nj) * dV\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 7
  },
  {
   "cell_type": "markdown",
   "source": [
    "Here we create a standard assembly routine."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "doassemble! (generic function with 1 method)"
     },
     "metadata": {},
     "execution_count": 8
    }
   ],
   "cell_type": "code",
   "source": [
    "function doassemble!(\n",
    "        cellvalues::CellValues, facetvalues::FacetValues, K::SparseMatrixCSC, f::Vector, dh::DofHandler, C::SymmetricTensor, q0::Float64, penalty::Float64\n",
    "    )\n",
    "\n",
    "    n = getnbasefunctions(cellvalues)\n",
    "    ke = zeros(n, n)\n",
    "    fe = zeros(n)\n",
    "\n",
    "    assembler = start_assemble(K, f)\n",
    "    for celldata in CellIterator(dh)\n",
    "        fill!(ke, 0.0)\n",
    "        fill!(fe, 0.0)\n",
    "        reinit!(cellvalues, celldata)\n",
    "        element_routine!(ke, fe, cellvalues, C, q0)\n",
    "        assemble!(assembler, celldofs(celldata), ke, fe)\n",
    "    end\n",
    "\n",
    "    ∂Ω = union(\n",
    "        getfacetset(grid, \"left\"),\n",
    "        getfacetset(grid, \"right\"),\n",
    "        getfacetset(grid, \"top\"),\n",
    "        getfacetset(grid, \"bottom\"),\n",
    "    )\n",
    "\n",
    "    for celldata in FacetIterator(dh, ∂Ω)\n",
    "        fill!(ke, 0.0)\n",
    "        reinit!(facetvalues, celldata)\n",
    "        bc_routine!(ke, facetvalues, penalty)\n",
    "        assemble!(assembler, celldofs(celldata), ke)\n",
    "    end\n",
    "    return\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 8
  },
  {
   "cell_type": "markdown",
   "source": [
    "Create stiffness matrix, assemble and solve:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "3886-element Vector{Float64}:\n -3.6627157009442177e-7\n  5.272923075374824e-5\n  5.2729230753807e-5\n -0.004704719162462571\n  0.09691827174410513\n -0.004704719162472541\n -7.187865225369506e-9\n -1.5349465695606945e-6\n  0.0100268745887488\n  0.0003147238992352744\n  ⋮\n -0.007086918388095675\n -3.662715700942211e-7\n -5.2729230753778115e-5\n -5.2729230753705866e-5\n -0.004704719162469942\n  0.09691827174406319\n -0.00470471916246031\n -0.005055033542012224\n  0.005055033542012229"
     },
     "metadata": {},
     "execution_count": 9
    }
   ],
   "cell_type": "code",
   "source": [
    "K = allocate_matrix(dh);\n",
    "f = zeros(ndofs(dh))\n",
    "doassemble!(cellvalues, facetvalues, K, f, dh, C, q0, penalty);\n",
    "u = K \\ f"
   ],
   "metadata": {},
   "execution_count": 9
  },
  {
   "cell_type": "markdown",
   "source": [
    "Export solution to VTK/Paraview"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "VTKGridFile for the closed file \"plate_equation.vtu\"."
     },
     "metadata": {},
     "execution_count": 10
    }
   ],
   "cell_type": "code",
   "source": [
    "VTKGridFile(\"plate_equation\", dh) do vtk\n",
    "    write_solution(vtk, dh, u)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 10
  },
  {
   "cell_type": "markdown",
   "source": [
    "To test the solution, we query the deflection at the center of the plate and compare it with the analtyical solution:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
     },
     "metadata": {},
     "execution_count": 11
    }
   ],
   "cell_type": "code",
   "source": [
    "mid_point = Vec((L / 2, L / 2))\n",
    "ph = PointEvalHandler(grid, [mid_point])\n",
    "w_fem = evaluate_at_points(ph, dh, u, :w) |> first #0.03548889438239366\n",
    "w_ana = w_analytical(mid_point, L, q0, D) #0.035488713207468166\n",
    "\n",
    "using Test\n",
    "@test w_fem ≈ w_ana atol = 1.0e-6"
   ],
   "metadata": {},
   "execution_count": 11
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}