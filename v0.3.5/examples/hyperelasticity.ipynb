{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Hyperelasticity\n",
    "\n",
    "**Keywords**: *hyperelasticity*, *finite strain*, *large deformations*, *Newton's method*,\n",
    "*conjugate gradient*, *automatic differentiation*\n",
    "\n",
    "![hyperelasticity.png](hyperelasticity.png)\n",
    "\n",
    "*Figure 1*: Cube loaded in torsion modeled with a hyperelastic material model and\n",
    "finite strain."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Introduction\n",
    "\n",
    "In this example we will solve a problem in a finite strain setting using an\n",
    "hyperelastic material model. In order to compute the stress we will use automatic\n",
    "differentiation, to solve the non-linear system we use Newton's\n",
    "method, and for solving the Newton increment we use conjugate gradients.\n",
    "\n",
    "The weak form is expressed in terms of the first Piola-Kirchoff stress $\\mathbf{P}$\n",
    "as follows: Find $u \\in \\mathbb{U}$ such that\n",
    "\n",
    "$$\n",
    "\\int_{\\Omega} \\nabla_{\\mathbf{X}} \\delta \\mathbf{u} : \\mathbf{P}(\\mathbf{u})\\ \\mathrm{d}\\Omega =\n",
    "\\int_{\\Omega} \\delta \\mathbf{u} \\cdot \\mathbf{b}\\ \\mathrm{d}\\Omega + \\int_{\\Gamma^\\mathrm{N}}\n",
    "\\delta \\mathbf{u} \\cdot \\mathbf{t}\\ \\mathrm{d}\\Gamma\n",
    "\\quad \\forall \\delta \\mathbf{u} \\in \\mathbb{U}^0,\n",
    "$$\n",
    "\n",
    "where $\\mathbf{u}$ is the unknown displacement field, $\\mathbf{b}$ is the body force acting\n",
    "on the reference domain, $\\mathbf{t}$ is the traction acting on the Neumann part of the reference\n",
    "domain's boundary, and where $\\mathbb{U}$ and $\\mathbb{U}^0$ are suitable trial and test sets.\n",
    "$\\Omega$ denotes the reference domain, which is also called reference or material domain.\n",
    "Gradients are defined with respect to the reference domain, here denoted with  an $X$.\n",
    "Formally this is expressed as $(\\nabla_{\\mathbf{X}} \\bullet)_{ij} := \\frac{\\partial(\\bullet)_i}{\\partial X_j}$.\n",
    "Note that for large deformation problems it is also possibile that gradients and integrals\n",
    "are defined on the deformed domain, which is also called the current or spatial domain, depending\n",
    "on the specific formulation."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Ferrite, Tensors, TimerOutputs, ProgressMeter\n",
    "import KrylovMethods, IterativeSolvers"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Hyperelastic material model\n",
    "\n",
    "The stress can be derived from an energy potential, defined in\n",
    "terms of the right Cauchy-Green tensor $\\mathbf{C} = \\mathbf{F}^{\\mathrm{T}} \\mathbf{F}$,\n",
    "where $\\mathbf{F} = \\mathbf{I} + \\nabla_{\\mathbf{X}} \\mathbf{u}$ is the deformation gradient.\n",
    "We shall use a neo-Hookean model, where the potential can be written as\n",
    "\n",
    "$$\n",
    "\\Psi(\\mathbf{C}) = \\frac{\\mu}{2} (I_1 - 3) - \\mu \\ln(J) + \\frac{\\lambda}{2} \\ln(J)^2,\n",
    "$$\n",
    "\n",
    "where $I_1 = \\mathrm{tr}(\\mathbf{C})$ is the first invariant, $J = \\sqrt{\\det(\\mathbf{C})}$\n",
    "and $\\mu$ and $\\lambda$ material parameters.\n",
    "From the potential we obtain the second Piola-Kirchoff stress $\\mathbf{S}$ as\n",
    "\n",
    "$$\n",
    "\\mathbf{S} = 2 \\frac{\\partial \\Psi}{\\partial \\mathbf{C}},\n",
    "$$\n",
    "\n",
    "and the tangent of $\\mathbf{S}$ as\n",
    "\n",
    "$$\n",
    "\\frac{\\partial \\mathbf{S}}{\\partial \\mathbf{C}} = 4 \\frac{\\partial \\Psi}{\\partial \\mathbf{C}}.\n",
    "$$\n",
    "\n",
    "We can implement the material model as follows, where we utilize automatic differentiation\n",
    "for the stress and the tangent, and thus only define the potential:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "struct NeoHooke\n",
    "    μ::Float64\n",
    "    λ::Float64\n",
    "end\n",
    "\n",
    "function Ψ(C, mp::NeoHooke)\n",
    "    μ = mp.μ\n",
    "    λ = mp.λ\n",
    "    Ic = tr(C)\n",
    "    J = sqrt(det(C))\n",
    "    return μ / 2 * (Ic - 3) - μ * log(J) + λ / 2 * log(J)^2\n",
    "end\n",
    "\n",
    "function constitutive_driver(C, mp::NeoHooke)\n",
    "    # Compute all derivatives in one function call\n",
    "    ∂²Ψ∂C², ∂Ψ∂C = Tensors.hessian(y -> Ψ(y, mp), C, :all)\n",
    "    S = 2.0 * ∂Ψ∂C\n",
    "    ∂S∂C = 4.0 * ∂²Ψ∂C²\n",
    "    return S, ∂S∂C\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "Finally, for the finite element problem we need $\\mathbf{P}$ and\n",
    "$\\frac{\\partial \\mathbf{P}}{\\partial \\mathbf{F}}$, which can be\n",
    "obtained by using the following relations:\n",
    "\n",
    "$$\n",
    "\\begin{align*}\n",
    "\\mathbf{P} &= \\mathbf{F} \\cdot \\mathbf{S},\\\\\n",
    "\\frac{\\partial \\mathbf{P}}{\\partial \\mathbf{F}} &= [\\mathbf{F} \\bar{\\otimes} \\mathbf{I}] :\n",
    "\\frac{\\partial \\mathbf{S}}{\\partial \\mathbf{C}} : [\\mathbf{F}^\\mathrm{T} \\bar{\\otimes} \\mathbf{I}]\n",
    "+ \\mathbf{I} \\bar{\\otimes} \\mathbf{S}.\n",
    "\\end{align*}\n",
    "$$\n",
    "\n",
    "## Newton's Method\n",
    "\n",
    "As mentioned above, to deal with the non-linear weak form we first linearize\n",
    "the problem such that we can apply Newton's method, and then apply the FEM to\n",
    "discretize the problem. Skipping a detailed derivation, Newton's method can\n",
    "be expressed as:\n",
    "Given some initial guess $\\mathbf{u}^0$, find a sequence $\\mathbf{u}^{k}$ by iterating\n",
    "\n",
    "$$\n",
    "\\mathbf{u}^{k+1} = \\mathbf{u}^{k} - \\Delta \\mathbf{u}^{k}\n",
    "$$\n",
    "\n",
    "until some termination condition has been met. Therein we determine $\\Delta \\mathbf{u}^{k}$\n",
    "\n",
    "$$\n",
    "\\mathbf{K}(\\mathbf{u}^{k}) \\Delta \\mathbf{u}^{k} = \\mathbf{g}(\\mathbf{u}^{k})\n",
    "$$\n",
    "\n",
    "where $\\mathbf{g}$ is the global residual and $\\mathbf{K} = \\frac{\\partial g}{\\partial u}$\n",
    "the Jacobi matrix, such that\n",
    "\n",
    "$$\n",
    "K_{ij} = \\int_{\\Omega} \\nabla_{\\mathbf{X}} \\delta \\mathbf{u}_{i} : \\frac{\\partial \\mathbf{P}}{\\partial \\mathbf{F}} : \\nabla_{\\mathbf{X}} \\delta \\mathbf{u}_{j} \\, \\mathrm{d} \\Omega\n",
    "$$\n",
    "and\n",
    "\n",
    "$$\n",
    "g_{i} = \\int_{\\Omega} \\nabla_{\\mathbf{X}} \\delta \\mathbf{u}_{i} : \\mathbf{P} - \\delta \\mathbf{u}_{i} \\cdot \\mathbf{b} \\, \\mathrm{d} \\Omega\n",
    "$$\n",
    "\n",
    "A detailed derivation can be found in every continuum mechanics book, which has a\n",
    "chapter about finite elasticity theory. We used \"Nonlinear solid mechanics: a continuum\n",
    "approach for engineering science.\" by Gerhard Holzapfel (chapter 8) as a reference.\n",
    "\n",
    "## Finite element assembly\n",
    "\n",
    "The element routine for assembling the residual and tangent stiffness is implemented\n",
    "as usual, with loops over quadrature points and shape functions:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function assemble_element!(ke, ge, cell, cv, fv, mp, ue)\n",
    "    # Reinitialize cell values, and reset output arrays\n",
    "    reinit!(cv, cell)\n",
    "    fill!(ke, 0.0)\n",
    "    fill!(ge, 0.0)\n",
    "\n",
    "    b = Vec{3}((0.0, -0.5, 0.0)) # Body force\n",
    "    t = Vec{3}((0.1, 0.0, 0.0)) # Traction\n",
    "    ndofs = getnbasefunctions(cv)\n",
    "\n",
    "    for qp in 1:getnquadpoints(cv)\n",
    "        dΩ = getdetJdV(cv, qp)\n",
    "        # Compute deformation gradient F and right Cauchy-Green tensor C\n",
    "        ∇u = function_gradient(cv, qp, ue)\n",
    "        F = one(∇u) + ∇u\n",
    "        C = tdot(F)\n",
    "        # Compute stress and tangent\n",
    "        S, ∂S∂C = constitutive_driver(C, mp)\n",
    "        P = F ⋅ S\n",
    "        I = one(S)\n",
    "        ∂P∂F = otimesu(F, I) ⊡ ∂S∂C ⊡ otimesu(F', I) + otimesu(I, S)\n",
    "\n",
    "        # Loop over test functions\n",
    "        for i in 1:ndofs\n",
    "            # Test function + gradient\n",
    "            δui = shape_value(cv, qp, i)\n",
    "            ∇δui = shape_gradient(cv, qp, i)\n",
    "            # Add contribution to the residual from this test function\n",
    "            ge[i] += ( ∇δui ⊡ P - δui ⋅ b ) * dΩ\n",
    "\n",
    "            ∇δui∂P∂F = ∇δui ⊡ ∂P∂F # Hoisted computation\n",
    "            for j in 1:ndofs\n",
    "                ∇δuj = shape_gradient(cv, qp, j)\n",
    "                # Add contribution to the tangent\n",
    "                ke[i, j] += ( ∇δui∂P∂F ⊡ ∇δuj ) * dΩ\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Surface integral for the traction\n",
    "    for face in 1:nfaces(cell)\n",
    "        if onboundary(cell, face)\n",
    "            reinit!(fv, cell, face)\n",
    "            for q_point in 1:getnquadpoints(fv)\n",
    "                dΓ = getdetJdV(fv, q_point)\n",
    "                for i in 1:ndofs\n",
    "                    δui = shape_value(fv, q_point, i)\n",
    "                    ge[i] -= (δui ⋅ t) * dΓ\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "Assembling global residual and tangent"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function assemble_global!(K, f, dh, cv, fv, mp, u)\n",
    "    n = ndofs_per_cell(dh)\n",
    "    ke = zeros(n, n)\n",
    "    ge = zeros(n)\n",
    "\n",
    "    # start_assemble resets K and f\n",
    "    assembler = start_assemble(K, f)\n",
    "\n",
    "    # Loop over all cells in the grid\n",
    "    @timeit \"assemble\" for cell in CellIterator(dh)\n",
    "        global_dofs = celldofs(cell)\n",
    "        ue = u[global_dofs] # element dofs\n",
    "        @timeit \"element assemble\" assemble_element!(ke, ge, cell, cv, fv, mp, ue)\n",
    "        assemble!(assembler, global_dofs, ge, ke)\n",
    "    end\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 4
  },
  {
   "cell_type": "markdown",
   "source": [
    "Define a main function, with a loop for Newton iterations"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "solve (generic function with 1 method)"
     },
     "metadata": {},
     "execution_count": 5
    }
   ],
   "cell_type": "code",
   "source": [
    "function solve()\n",
    "    reset_timer!()\n",
    "\n",
    "    # Generate a grid\n",
    "    N = 10\n",
    "    L = 1.0\n",
    "    left = zero(Vec{3})\n",
    "    right = L * ones(Vec{3})\n",
    "    grid = generate_grid(Tetrahedron, (N, N, N), left, right)\n",
    "\n",
    "    # Material parameters\n",
    "    E = 10.0\n",
    "    ν = 0.3\n",
    "    μ = E / (2(1 + ν))\n",
    "    λ = (E * ν) / ((1 + ν) * (1 - 2ν))\n",
    "    mp = NeoHooke(μ, λ)\n",
    "\n",
    "    # Finite element base\n",
    "    ip = Lagrange{3, RefTetrahedron, 1}()\n",
    "    qr = QuadratureRule{3, RefTetrahedron}(1)\n",
    "    qr_face = QuadratureRule{2, RefTetrahedron}(1)\n",
    "    cv = CellVectorValues(qr, ip)\n",
    "    fv = FaceVectorValues(qr_face, ip)\n",
    "\n",
    "    # DofHandler\n",
    "    dh = DofHandler(grid)\n",
    "    push!(dh, :u, 3) # Add a displacement field\n",
    "    close!(dh)\n",
    "\n",
    "    function rotation(X, t, θ = deg2rad(60.0))\n",
    "        x, y, z = X\n",
    "        return t * Vec{3}(\n",
    "            (0.0,\n",
    "            L/2 - y + (y-L/2)*cos(θ) - (z-L/2)*sin(θ),\n",
    "            L/2 - z + (y-L/2)*sin(θ) + (z-L/2)*cos(θ)\n",
    "            ))\n",
    "    end\n",
    "\n",
    "    dbcs = ConstraintHandler(dh)\n",
    "    # Add a homogenous boundary condition on the \"clamped\" edge\n",
    "    dbc = Dirichlet(:u, getfaceset(grid, \"right\"), (x,t) -> [0.0, 0.0, 0.0], [1, 2, 3])\n",
    "    add!(dbcs, dbc)\n",
    "    dbc = Dirichlet(:u, getfaceset(grid, \"left\"), (x,t) -> rotation(x, t), [1, 2, 3])\n",
    "    add!(dbcs, dbc)\n",
    "    close!(dbcs)\n",
    "    t = 0.5\n",
    "    Ferrite.update!(dbcs, t)\n",
    "\n",
    "    # Pre-allocation of vectors for the solution and Newton increments\n",
    "    _ndofs = ndofs(dh)\n",
    "    un = zeros(_ndofs) # previous solution vector\n",
    "    u  = zeros(_ndofs)\n",
    "    Δu = zeros(_ndofs)\n",
    "    ΔΔu = zeros(_ndofs)\n",
    "    apply!(un, dbcs)\n",
    "\n",
    "    # Create sparse matrix and residual vector\n",
    "    K = create_sparsity_pattern(dh)\n",
    "    g = zeros(_ndofs)\n",
    "\n",
    "\n",
    "    # Perform Newton iterations\n",
    "    newton_itr = -1\n",
    "    NEWTON_TOL = 1e-8\n",
    "    prog = ProgressMeter.ProgressThresh(NEWTON_TOL, \"Solving:\")\n",
    "\n",
    "    while true; newton_itr += 1\n",
    "        u .= un .+ Δu # Current guess\n",
    "        assemble_global!(K, g, dh, cv, fv, mp, u)\n",
    "        normg = norm(g[Ferrite.free_dofs(dbcs)])\n",
    "        apply_zero!(K, g, dbcs)\n",
    "        ProgressMeter.update!(prog, normg; showvalues = [(:iter, newton_itr)])\n",
    "\n",
    "        if normg < NEWTON_TOL\n",
    "            break\n",
    "        elseif newton_itr > 30\n",
    "            error(\"Reached maximum Newton iterations, aborting\")\n",
    "        end\n",
    "\n",
    "        # Compute increment using cg! from IterativeSolvers.jl\n",
    "        @timeit \"linear solve (KrylovMethods.cg)\" ΔΔu′, flag, relres, iter, resvec = KrylovMethods.cg(K, g; maxIter = 1000)\n",
    "        @assert flag == 0\n",
    "        @timeit \"linear solve (IterativeSolvers.cg!)\" IterativeSolvers.cg!(ΔΔu, K, g; maxiter=1000)\n",
    "\n",
    "        apply_zero!(ΔΔu, dbcs)\n",
    "        Δu .-= ΔΔu\n",
    "    end\n",
    "\n",
    "    # Save the solution\n",
    "    @timeit \"export\" begin\n",
    "        vtk_grid(\"hyperelasticity\", dh) do vtkfile\n",
    "            vtk_point_data(vtkfile, dh, u)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    print_timer(title = \"Analysis with $(getncells(grid)) elements\", linechars = :ascii)\n",
    "    return u\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 5
  },
  {
   "cell_type": "markdown",
   "source": [
    "Run the simulation"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\rSolving: (thresh = 1e-08, value = 0.194526)\u001b[K\r\n",
      "  iter:  1\u001b[K\r\u001b[A\n",
      "\r\u001b[K\u001b[A\rSolving: Time: 0:00:00 (6 iterations)\u001b[K\r\n",
      "  iter:  5\u001b[K\n",
      " --------------------------------------------------------------------------------\n",
      "  Analysis with 6000 elements           Time                    Allocations      \n",
      "                               -----------------------   ------------------------\n",
      "       Tot / % measured:            3.13s /  36.0%            216MiB /   7.1%    \n",
      "\n",
      " Section               ncalls     time    %tot     avg     alloc    %tot      avg\n",
      " --------------------------------------------------------------------------------\n",
      " export                     1    795ms   70.5%   795ms   6.28MiB   41.1%  6.28MiB\n",
      " assemble                   6    165ms   14.6%  27.5ms   7.63MiB   49.9%  1.27MiB\n",
      "   element assemble     36.0k   78.6ms    7.0%  2.18μs     0.00B    0.0%    0.00B\n",
      " linear solve (Iter...      5    118ms   10.4%  23.5ms    582KiB    3.7%   116KiB\n",
      " linear solve (Kryl...      5   49.8ms    4.4%  10.0ms    824KiB    5.3%   165KiB\n",
      " --------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "u = solve();"
   ],
   "metadata": {},
   "execution_count": 6
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.3"
  },
  "kernelspec": {
   "name": "julia-1.7",
   "display_name": "Julia 1.7.3",
   "language": "julia"
  }
 },
 "nbformat": 4
}
